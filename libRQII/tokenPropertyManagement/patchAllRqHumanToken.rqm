[h: arguments = macro.args]
[h, if(json.isEmpty(arguments)): mapName = getCurrentMapName(); mapName = json.get(arguments,"mapName")]

[h: cond = json.set("{}","propertyType" ,"RQ_Human","mapName",mapName)]
[h: rqHumanTokens = getTokens("json", cond)]

<h3>Map :: [r: mapName]</h3><br>
<b>Start combatStatus property patching </b>
<hr>
[r, foreach(tokenId, rqHumanTokens, ""), code:{
	[h: currentCombatStatus = getProperty("combatStatus",tokenId, mapName)]
	[r, if(json.isEmpty(currentCombatStatus)), code:{
		<br><br><b>[r: getName(tokenId, mapName)] have no combatStatus prop !</b><br>
		[h: currentCombatStatus = json.set("{}","versionTag","v0.0.0")]
	};{}]
	[h: "<!-- Check version -->"]
	[h: currentVersion = json.get(currentCombatStatus,"versionTag")]
	
	[h, if(currentVersion == ""), code:{
  		[h: propspellReady = json.get(currentCombatStatus,"spellReady")]
  		[h, if(propspellReady != "") : currentVersion = "v2.0.0";currentVersion = "v1.0.0"]
	}]
	[r, if(currentVersion != "v3.0.0"), code:{
	  <br>[r: getName(tokenId, mapName)] is going to be patched 
	  [h: macroParam = json.set("{}", "tokenId", tokenId,"mapName",mapName)]
	  - [r, macro("patchPropCombatStatus@Lib:RQII"): macroParam]  
	}]
}]

<br><br>
<b>Non RQ_Human tokens are not patched</b>
<hr>
[h: cond = json.set("{}","mapName",mapName)]
[h: tokenIds = getTokens("json", cond)]
[r, foreach(tokenId, tokenIds, ""), code:{

[h: tokenType = getPropertyType(tokenId, mapName)]
[r, if(tokenType != "RQ_Human"),code:{
	<br>[r: getName(tokenId,mapName)] is of type : [r: tokenType]
}]
	
}]