[h: arguments = macro.args]
[r, if(json.isEmpty(arguments)), code: {
  [assert(0,"<i>locHpAp::viewCurrentAPHP</i> : no arguments, this macro can't be called without mandatory parameters",0)]
 };{
  [h: tokenId = json.get(arguments,"tokenId")]
}]

[h: pLocD20 = getProperty("locD20", tokenId)]
[h: pLocName= getProperty("locName", tokenId)]
[h: pLocAP = getProperty("locAP", tokenId)]
[h: pLocAPorig = getProperty("locAPorig", tokenId)]
[h: pLocHP = getProperty("locHP", tokenId)]
[h: pLocHPorig = getProperty("locHPorig", tokenId)]
[h: pLocNb = getProperty("locNb", tokenId)]

<table id="inner" class="content pure-table pure-table-striped table-striped-custom">
  <thead>
  <tr>
    <th>Location</th>
    [for(i,1,pLocNb+1,1,""), code:{
      <th>
        [r: getStrProp(pLocName, strformat("loc%{i}"))]<br>
        ([r: getStrProp(pLocD20, strformat("loc%{i}"))])
      </th>
    }]
  </tr>
  </thead>
  <tbody>
  <tr>
    <td style="text-align:left;">AP</td>
    [for(i,1,pLocNb+1,1,""), code:{
        <td>
        [h: locAPOrigValue = getStrProp(pLocAPorig, strformat("loc%{i}"))]
        [h: locAPCurrentValue = getStrProp(pLocAP, strformat("loc%{i}"))]
        [r, if ((locAPCurrentValue < locAPOrigValue) &&(locAPCurrentValue >0)), code: {
            <span style="font-weight: bold;color:blue">[r: locAPCurrentValue]</span>
        };{}]
        [r, if ((locAPCurrentValue <= 0)&&(locAPOrigValue>0)), code: {
            <span style="font-weight: bold;color:red">[r: locAPCurrentValue]</span>
        };{}]
        [r, if (locAPCurrentValue == locAPOrigValue), code: {
          [r: locAPCurrentValue]
        };{}]
      </td>
    }]
  </tr>
  <tr>
    <td style="text-align:left;">HP</td>
    [for(i,1,pLocNb+1,1,""), code:{
        <td>
          [h: locOrigValue = getStrProp(pLocHPorig, strformat("loc%{i}"))]
          [h: locCurrentValue = getStrProp(pLocHP, strformat("loc%{i}"))]
          [r, if((locCurrentValue < locOrigValue)&&(locCurrentValue >0)), code: {
              <span style="font-weight: bold;color:blue">[r: locCurrentValue]</span>
          };{}]
          [r, if((locCurrentValue <= 0)&&(locCurrentValue + locOrigValue)>0), code: {
              <span style="font-weight: bold;color:orange">[r: locCurrentValue]</span>
          };{}]
          [r, if((locCurrentValue + locOrigValue) <= 0), code: {
              <span style="font-weight: bold;color:red">[r: locCurrentValue]</span>
          };{}]
          [r, if(locCurrentValue == locOrigValue), code: {
              [r: locCurrentValue]
          };{}]
      </td>
        }]
  </tr>
  <tr>
    <td>Wound</td>
    [for(i,1,pLocNb+1,1,""), code:{
        <td>
           [h: locOrigValue = getStrProp(pLocHPorig, strformat("loc%{i}"))]
           [h: locCurrentValue = getStrProp(pLocHP, strformat("loc%{i}"))]
           [r, if((locCurrentValue < locOrigValue)&&(locCurrentValue >0)), code: {
              <span style="font-weight: bold;color:blue">L</span>
           };{}]
           [r, if((locCurrentValue <= 0)&&(locCurrentValue + locOrigValue)>0), code: {
              <span style="font-weight: bold;color:orange">S</span>
           };{}]
           [r, if((locCurrentValue + locOrigValue) <= 0), code: {
              <span style="font-weight: bold;color:red">M</span>
           };{}]
      </td>
      }]
  </tr>
  </tbody>  
</table>