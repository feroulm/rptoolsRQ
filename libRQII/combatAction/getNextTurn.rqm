<!-- Function to get next token which can play in the current cycle -->
<!-- return a TokenId or 0 if no token is available -->
[h: tokenId = arg(0)] 
[h: initList = getInitiativeList()]
[h: tokenOffset = getCurrentInitiative()]
[h: currentOffset = 0]
[h: maxOffset = initiativeSize()]

[h: tokenFound = 0]
[h: tokenNotFound = 1]
[h: tokenOut = 1]
[h: loopEnd = 0]
<!-- start loop after current token unless it is the last -->
[h: nextOffset = tokenOffset + 1]
[h, if(nextOffset == maxOffset): loopEnd = 1]

<!-- search token after him -->
[h, while (loopEnd == 0), code:
{
	[h: jsonPath = "tokens.["+nextOffset+"].tokenId"]
	[h: nextTokenId = json.path.read(initList, jsonPath)]
	[h: currentCombatStatus = getProperty("combatStatus",nextTokenId)]
	[h: turnStatus = json.get(currentCombatStatus,"turnStatus")]
	[h, if(turnStatus == "on"), code:{
		[h: tokenFound = 1]	
		[h: tokenNotFound = 0]	
		[h: loopEnd = 1]
		[h: nextTokenOffset = nextOffset]	
	}]
	[h, if(turnStatus == "out" || turnStatus == "disabled"), code:{
		[h: tokenOut = tokenOut + 1]		
	}]
	[h, if(nextOffset+1 == maxOffset): loopEnd = 1]
	[h: nextOffset = nextOffset + 1]
}]
[r, if(tokenFound), code:{
  [h: linkUpdateCombat = macroLinkText("updateCombat@Lib:RQII", "all")]
  <form action="[r:linkUpdateCombat]" method="json">
        <input type="hidden" name="tokenId" value="[r:tokenId]">
        <input type="hidden" name="action" value="nxtturn">
        <input type="hidden" name="nextTokenId" value="[r:nextTokenId]">
        <input type="hidden" name="nextTokenOffset" value="[r:nextTokenOffset]">
        <input type="submit" name="edit_btn" value="next turn">
  </form>
}]
[r, if(tokenOut == maxOffset), code:{
  [h: tokenNotFound = 0]	
  [h: tokens = json.get(initList,"tokens")]
  [h: linkUpdateGlobalCombat = macroLinkText("updateGlobalCombat@Lib:RQII", "all")]
  <form action="[r:linkUpdateGlobalCombat]" method="json">
    <input type="hidden" name="tokens" value=[r: tokens]>
    <input type="hidden" name="action" value="nxtmr">
    <input type="submit" name="edit_btn" value="Next MR">
  </form>
}]
[r, if(tokenNotFound), code:{
  [h: tokens = json.get(initList,"tokens")]
  [h: linkUpdateGlobalCombat = macroLinkText("updateGlobalCombat@Lib:RQII", "all")]
  <form action="[r:linkUpdateGlobalCombat]" method="json">
    <input type="hidden" name="tokens" value=[r: tokens]>
    <input type="hidden" name="action" value="nxtcycle">
    <input type="submit" name="edit_btn" value="Next Cycle">
  </form>
}]