[h: tokenId = json.get(macro.args, "tokenId")]
[h: action = json.get(macro.args, "action")]
[h: actionLabel = json.get(macro.args, "edit_btn")]

[h: cLogTokenId = "none"]
[h: cLogTokenId = json.get(macro.args,"cLogTokenId")]
[h, if(json.isEmpty(cLogTokenId)) : cLogTokenId = "none"]

[h: currentCombatStatus = getProperty("combatStatus",tokenId)]
[h: activeCA = json.get(currentCombatStatus,"activeCA")]
[h: reactiveCA = json.get(currentCombatStatus,"reactiveCA")]
[h: lostProCA = json.get(currentCombatStatus,"lostProCA")]
[h: firstEvade = json.get(currentCombatStatus,"firstEvade")]

[r, switch(action), code:
  case "parry" : {
  
  	[h: msg = actionLabel+" is done, one reactive CA lost."]
    [h: logMsg = actionLabel]
  	
    [h: currentCombatStatus = processReactiveCA(currentCombatStatus,tokenId)]
    [h: setProperty("combatStatus",currentCombatStatus,tokenId)]
  };
  case "evade" : {
    <!-- toggle evade one time on two -->
    [h, if (firstEvade == 0),code:{
    	[h: currentCombatStatus = json.set(currentCombatStatus,"firstEvade",1)]
    	[h: msg = "First "+actionLabel+" is done, next proactive CA lost."]
    	[h: logMsg = "First "+actionLabel]
    }]
    [h, if (firstEvade > 0),code:{
    	[h: currentCombatStatus = json.set(currentCombatStatus,"firstEvade",0)]
    	[h: msg = "Second "+actionLabel+" is done, proactive action possible next turn."]
    	[h: logMsg = "Second "+actionLabel]
    }]

    [h: currentCombatStatus = processReactiveCA(currentCombatStatus,tokenId)]
    [h: setProperty("combatStatus",currentCombatStatus,tokenId)]	
  };
  case "evadeProne" : {
    [h: currentCombatStatus = json.set(currentCombatStatus,"situationMod","prone")]
    [h: setState("Prone", 1, tokenId)]

    [h: msg = "Evade is done, token is prone !"]
    [h: logMsg = actionLabel]
	
	[h: currentCombatStatus = processReactiveCA(currentCombatStatus,tokenId)]
	[h: setProperty("combatStatus",currentCombatStatus,tokenId)]
  };
  case "interrupt" : {
  	<!-- remove current initiative to current token -->
  	[h: oldTokenId = getInitiativeToken()]
  	[h: setState("CurrentTurn", 0, oldTokenId)]
  	[h: oldTokenCombatStatus = getProperty("combatStatus",oldTokenId)]
  	[h: oldTokenCombatStatus = json.set(oldTokenCombatStatus,"activeTurn",0)]
  	[h: setProperty("combatStatus",oldTokenCombatStatus,oldTokenId)]
  	<!-- get current token offset in order to set Current Initiative (which is using only offset ...)-->
  	[h: newTokenOffset = getInitiativeTokenOffset(tokenId)]
   	[h: setCurrentInitiative(newTokenOffset)]
    [h: currentCombatStatus = json.set(currentCombatStatus,"turnStatus","on")]
   	[h: currentCombatStatus = json.set(currentCombatStatus,"activeTurn",1)]
   	[h: setProperty("combatStatus",currentCombatStatus,tokenId)]
   	[h: setState("CurrentTurn", 1, tokenId)]
   	<!-- Refresh / Open Combat Action Window for current Active Token-->
   	[h: msg = "Interrupt is done, take your turn"]
   	[h: logMsg = actionLabel]
  };
  default: {
 	<!-- do nothing -->
 	<br><b>[r: action]</b> is not implemented in <i>updateCombatReactive</i> !<br>
 }
]

<!-- log the action -->
[h: logCombatAction(tokenId,"reactive",logMsg)]
[h: addCombatLogEntry(tokenId,"reactiveAction",logMsg,"")]
    
[r: getName(tokenId)] has done : [r: logMsg]

<!-- Refresh Combat Action Window for current Active Token-->
[h: macroParam = json.set("{}", "tokenId", tokenId,"msg",msg,"cLogTokenId", cLogTokenId)]
[h: lnkTxt = macroLinktext("openCombatAction@Lib:RQII", "all",macroParam)]
[h: execLink(lnkTxt,0,"all")]
    
<!-- Refresh Global Combat Window -->
[h: macroParam = json.set("{}", "tab", "main","cLogTokenId", cLogTokenId)]
[h: lnkTxt = macroLinktext("openCombat@Lib:RQII", "all",macroParam)]
[h: execLink(lnkTxt,0,"all")]
[h: macroParam = json.set("{}", "tab", "main","cLogTokenId", cLogTokenId)]
[h: lnkTxt = macroLinktext("openCombatSheets@Lib:RQII", "all",macroParam)]
[h: execLink(lnkTxt,0,"all")]