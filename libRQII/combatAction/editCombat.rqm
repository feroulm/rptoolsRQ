[h: arguments = macro.args]
[h: tokens = json.get(arguments, "tokens")]
[h: linkCA = macroLinkText("openCombatAction@Lib:RQII", "all")]

[h:"<!-- get current combat token -->"]
[h: cLogTokenId = findCurrentCombatToken()]

[h, if(cLogTokenId == "none"), code :{
  [h: combatProp = json.set("{}", "status","none","desc","! NO CURRENT COMBAT FOUND, you need to create one first")]
  [h: noClogToken = 1]
};{
  [h: combatProp = getProperty("combat",cLogTokenId)]
  [h: noClogToken = 0]
}]

[h: linkUpdateGlobalCombat = macroLinkText("updateGlobalCombat@Lib:RQII", "all")]
[h: initRoll = json.get(combatProp, "initRoll")]

<div class="contentFrame">
[r, if(cLogTokenId != "none"), code:{
	<table>
		<tr>
			<td>Initiative</td>
			<td>Cycle</td>
			<td>Melee Round</td>
			<td><i>Combat Name</i> : [r: json.get(combatProp,"desc")]</td>
		</tr>
		<tr>
			<td>
				[r, if(initRoll > 0): "Rolled "+initRoll+" time(s)";"Not rolled !"]
			</td>
			<td><b>[r: json.get(combatProp, "cycle")]</b></td>
			<td><b>[r: json.get(combatProp, "mr")]</b></td>
			<td rowspan="2">
				<form action="[r:linkUpdateGlobalCombat]" method="json">
					<input type="hidden" name="tokens" value=[r: tokens]>
					<input type="hidden" name="action" value="reset">
					<input type="hidden" name="cLogTokenId" value=[r: cLogTokenId]>
					<input type="submit" name="edit_btn" value="Start / Reset">
				</form>
				[r: macroLink("Refresh", "openCombat@Lib:RQII", "all")]
			</td>
		</tr>
		<tr>
			<td>
				<form action="[r:linkUpdateGlobalCombat]" method="json">
					<input type="hidden" name="tokens" value=[r: tokens]>
					<input type="hidden" name="action" value="rollinit">
					<input type="submit" name="edit_btn" value="Roll Initiative">
				</form>
			</td>    
			<td>
				<form action="[r:linkUpdateGlobalCombat]" method="json">
					<input type="hidden" name="tokens" value=[r: tokens]>
					<input type="hidden" name="action" value="nxtcycle">
					<input type="submit" name="edit_btn" value="Next Cycle">
				</form>
			</td>
			<td>
				<form action="[r:linkUpdateGlobalCombat]" method="json">
					<input type="hidden" name="tokens" value=[r: tokens]>
					<input type="hidden" name="action" value="nxtmr">
					<input type="submit" name="edit_btn" value="Next MR">
				</form>
			</td>
		</tr>
	</table>
};{
	 No current Combat on this map - Create a combat first
	  [h: macroParam = json.set("{}", "tokens", tokens, "action", "reset","cLogTokenId",cLogTokenId)]
		[h: link = macroLinkText("updateGlobalCombat@Lib:RQII", "all",macroParam)]
		<a class="button-warning pure-button" href="[r: link]">Create new combat</a>
}]
<hr>
<table style="cellspacing:0px;">
  <tr>
    <td style="text-align:left">Token</td>
    <td style="width:30px">CA</td>
    <td style="text-align:center">Turn status</td>
	<td style="text-align:center">Condition</td>
    <td></td>
    <td style="text-align:left">Last proactive action</td>
    <td style="text-align:left">Last reactive action</td>
    <td style="text-align:left">Last change after action</td>
    <td style="text-align:left">Turn Declaration</td>
  </tr>
  [h: class = "oddRow"]
[r, foreach(item, tokens, ""), code: {
  [h: tokenId = json.get(item, "tokenId")]
  [h: currentCombatStatus = getProperty("combatStatus",tokenId)]
  [h: currentCombatLog = getProperty("combatLog",tokenId)]
  [h: weaponReach = json.get(currentCombatStatus,"weaponReach")]
  [h: activeTurn = json.get(currentCombatStatus,"activeTurn")]
  [h: situationMod = json.get(currentCombatStatus,"situationMod")]
  [h: chargeStatus = json.get(currentCombatStatus,"chargeStatus")]

  <!-- concat various condition situationMod, range, charging -->
  [h: tokenOtherStatus = ""]
  [h, if(weaponReach != "ok"): tokenOtherStatus=tokenOtherStatus+"<li>"+weaponReach+"</li>"]
  [h, if(situationMod != "none"): tokenOtherStatus=tokenOtherStatus+"<li>"+situationMod+"</li>"]
  [h, if(chargeStatus != "none"): tokenOtherStatus=tokenOtherStatus+"<li>"+chargeStatus+"</li>"]
  
  [h,if(activeTurn) : class = "activeRow"]
  <tr class="[r:class]">
  	[h: macroParam = json.set("{}", "tokenId", tokenId)]
    [h: lnkTxt = macroLinktext("openSheet@Lib:RQII", "all",macroParam)]
    <td style="text-align:left"> <a class="pure-menu-link" href="[r: lnkTxt]">[r: getName(tokenId)]</a> </td>
    <td> [r: json.get(currentCombatStatus,"activeCA")] / <b>[r: json.get(currentCombatStatus,"ccCA")]</b> </td>
    <td> [r: json.get(currentCombatStatus,"turnStatus")]</td>
	<td style="text-align:left"><ul>[r: tokenOtherStatus ]</ul></td>
    <td style="padding: 0px; cellspacing:0px;">
      <form action="[r:linkCA]" method="json">
        <input type="hidden" name="tokenId" value="[r:tokenId]">
        <input type="hidden" name="msg" value="">
        <input type="submit" name="edit_btn" value="act">
      </form>
    </td>
    <td style="text-align:left">[r: json.get(currentCombatLog,"lastProactiveAction")]</td>
    <td style="text-align:left">[r: json.get(currentCombatLog,"lastReactiveAction")]</td> 
    <td style="text-align:left">[r: json.get(currentCombatLog,"lastTokenChange")]</td> 
    <td style="text-align:left">[r: json.get(currentCombatLog,"turnDeclaration")]</td>
  </tr>
  [h: class = if(class=="oddRow", "evenRow", "oddRow")]
}]
</table>
</div>