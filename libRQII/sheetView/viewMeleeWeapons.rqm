[h: arguments = macro.args]
[h: tokenId = json.get(arguments, "tokenId")]
[h: srcWindow = json.get(arguments, "srcWindow")]

[h: propDamageMod = getProperty("DamageMod",tokenId)]
[h: tokenDmgMod = json.get(propDamageMod, "current")]


[h, if(tokenDmgMod != "0"), code:{
  [h: tokenDmgModDisplay = "<span style='color:red'>"+tokenDmgMod+"</span>"]
};{
  [h: tokenDmgModDisplay = ""]
}]

[h: wpList = getWeaponNbLst(tokenId)]
<table id="inner" class="content pure-table pure-table-striped table-striped-custom">
  <thead>
    <tr>
      <th>Name(<i>Type</i>)</th>
      <th>Size</th>
      <th>Reach</th>
      <th>Damage</th>
      <th>AP/HP</th>
      <th>Manoeuvre</th>
      <th>Special</th>
      <th>Hold</th>
    </tr>
  </thead>
  <tbody>
    [foreach(weapon, wpList, ""), code: {
      [h,macro("getWeapon@Lib:RQII"): "weaponNb="+weapon+"; tokId="+tokenId]
      [h: wProp = macro.return]
      [h: isHold = getStrProp(wProp, "Hold", 0)]
      [h,if(isHold) : cssSelector = "hold"; cssSelector = ""]
      <tr>
        <td class="[r:cssSelector]">
          <b>[r: getStrProp(wProp, "Name")](<i>[r: getStrProp(wProp, "Type")]</i>)</b>
        </td>
        <td>[r: getStrProp(wProp, "Size")]</td>
        <td>[r: getStrProp(wProp, "Reach")]</td>
        <td>[r: getStrProp(wProp, "Damage")+tokenDmgModDisplay]</td>
        <td>[r: getStrProp(wProp, "ApHp")]</td>
        <td>[r: getStrProp(wProp, "Manoeuvre")]</td>
        <td>[r: getStrProp(wProp, "Special")]</td>
        <td>
          [r, if(isHold), code:{
            [h: macroParam = json.set("{}", "tokenId", tokenId,"weaponId",weapon,"weaponfamily","melee","holdValue",0,"srcWindow",srcWindow)]
            [h: lnkTxt = macroLinktext("updateHoldWeapon@Lib:RQII", "all",macroParam)]
            <a class="pure-button button-warning button-small" href="[r: lnkTxt]">-</a>
          };{
            [h: macroParam = json.set("{}", "tokenId", tokenId,"weaponId",weapon,"weaponfamily","melee","holdValue",1,"srcWindow",srcWindow)]
            [h: lnkTxt = macroLinktext("updateHoldWeapon@Lib:RQII", "all",macroParam)]
            <a class="pure-button button-secondary button-small" href="[r: lnkTxt]">+</a>
          }]
        </td>
      </tr>
    }]
  </tbody>
</table>