[h: arguments = macro.args]
[h: tokenId = json.get(arguments, "tokenId")]
[h: srcWindow = json.get(arguments, "srcWindow")]

[h: propDamageMod = getProperty("DamageMod",tokenId)]
[h: tokenDmgMod = json.get(propDamageMod, "current")]

[h: wpList = getMissileNbLst(tokenId)]
<table id="inner" class="content pure-table pure-table-striped table-striped-custom">
  <thead>
  <tr>
    <th>Name(<i>Type</i>)</th>
    <th>Size</th>
    <th>Load</th>
    <th>Reach</th>
    <th>Damage</th>
    <th>Modif</th>
    <th>AP/HP</th>
    <th>Manoeuvre</th>
    <th>Special</th>
    <th>Hold</th>
  </tr>
  </thead>
  <tbody>
    [foreach(missile, wpList, ""), code: {
      [h,macro("getMissile@Lib:RQII"): "weaponNb="+missile+"; tokId="+tokenId]
      [h: wProp = macro.return]
      [h: missileDmgMod = getStrProp(wProp, "Modif")]
      [h, if((tokenDmgMod != "0")&&(missileDmgMod == "Y")), code:{
        [h: tokenDmgModDisplay = "<span style='color:red'>"+tokenDmgMod+"</span>"]
      };{
        [h: tokenDmgModDisplay = ""]
      }]
      [h: isHold = getStrProp(wProp, "Hold", 0)]
      [h,if(isHold) : cssSelector = "hold"; cssSelector = ""]
      <tr>
        <td class="[r:cssSelector]">
          <b>[r: getStrProp(wProp, "Name")]</b>(<i>[r: getStrProp(wProp, "Type")]</i>)
        </td>
        <td>[r: getStrProp(wProp, "Size")]</td>
        <td>[r: getStrProp(wProp, "Load")]</td>
        <td>[r: getStrProp(wProp, "Reach")]</td>
        <td>[r: getStrProp(wProp, "Damage")+tokenDmgModDisplay]</td>
        <td>[r: getStrProp(wProp, "Modif")]</td>
        <td>[r: getStrProp(wProp, "ApHp")]</td>
        <td>[r: getStrProp(wProp, "Manoeuvre")]</td>
        <td>[r: getStrProp(wProp, "Special")]</td>
        <td>
          [r, if(isHold), code:{
            [h: macroParam = json.set("{}", "tokenId", tokenId,"weaponId",missile,"weaponfamily","missile","holdValue",0,"srcWindow",srcWindow)]
            [h: lnkTxt = macroLinktext("updateHoldWeapon@Lib:RQII", "all",macroParam)]
            <a class="pure-button button-warning button-small" href="[r: lnkTxt]"><b>-</b></a>
          };{
            [h: macroParam = json.set("{}", "tokenId", tokenId,"weaponId",missile,"weaponfamily","missile","holdValue",1,"srcWindow",srcWindow)]
            [h: lnkTxt = macroLinktext("updateHoldWeapon@Lib:RQII", "all",macroParam)]
            <a class="pure-button button-secondary button-small" href="[r: lnkTxt]"><b>+</b></a>
          }]
        </td>
      </tr>
    }]
  </tbody>
</table>