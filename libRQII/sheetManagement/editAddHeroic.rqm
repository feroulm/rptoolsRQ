[h: arguments = macro.args] 
[h: tokenId = json.get(arguments, "tokenId")]
[h: nameFilter = json.get(arguments, "nameFilter")]
[h: typeFilter = json.get(arguments, "typeFilter")]

[h, if(nameFilter == "") : nameFilter = ".*"]
[h, if(typeFilter == "") : typeFilter = ".*"]

[h: heroicDb = getLibProperty("heroicDb","Lib:RQII")]

[h: link = macroLinkText("openAddHeroic@Lib:RQII", "all")]
<form action="[r:link]" method="json">
  <input type="hidden" name="tokenId" value="[r:tokenId]">
  <table>
    <tr>
       <th>Name</th>
       <th>Type</th>
       <th></th>
    </tr>
    <tr>
      <td><input type="text" name="nameFilter" value="[r: nameFilter]"></td>
      <td><input type="text" name="typeFilter" value="[r: typeFilter]"></td>
      <td><input type="submit" name="edit_btn" value="Filter"></td>
    </tr>
  </table>
</form>

[h: linkAddHeroic = macroLinkText("addHeroic@Lib:RQII", "all")]
<form action="[r:linkAddHeroic]" method="json">
<input type="hidden" name="tokenId" value="[r:tokenId]">
<table id="stats" cellpadding="2" cellspacing="0" border="0">
  [h: class = "oddRow"]
  [r, foreach(heroic, heroicDb, ""), code:{
	[h: heroicAttr = json.get(heroicDb,heroic)]
	[h: heroicName = json.get(heroicAttr,"displayname")]
	[h: heroicType = json.get(heroicAttr,"type")]
	[r, if(matches(heroicName,nameFilter) && matches(heroicType,typeFilter)), code:{
	<tr class="[r:class]">
	  <td class="leftBorder">
        <input type="checkbox" name="[r:heroic]"/>
      </td>
      <td>
        [r: heroicName] 
      </td>
      <td>
        [r: json.get(heroicAttr,"type")]
      </td>
      <td>
        [r: json.get(heroicAttr,"requirements")]
      </td>
      <td>
        [r: json.get(heroicAttr,"heroPoints")]
      </td>
      <td>
        [r: json.get(heroicAttr,"duration")]
      </td>
      <td>
        [r: json.get(heroicAttr,"lnk")]
      </td>
      <td>
        [r: json.get(heroicAttr,"desc")]
      </td>    
    </tr>
	[h: class = if(class=="oddRow", "evenRow", "oddRow")] 
	};{}]
  }]
</table>
<input type="submit" name="edit_btn" value="Add Heroic">
</form>