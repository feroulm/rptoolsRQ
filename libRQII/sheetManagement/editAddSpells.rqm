[h: arguments = macro.args] 
[h: tokenId = json.get(arguments, "tokenId")]
[h: nameFilter = json.get(arguments, "nameFilter")]
[h: typeFilter = json.get(arguments, "typeFilter")]

[h, if(nameFilter == "") : nameFilter = ".*"]
[h, if(typeFilter == "") : typeFilter = "common"]

[r, switch(typeFilter), code:
  case "common" :{
  	[h: spellsDb = getLibProperty("spellsDb", "Lib:RQII")]
  };
  case "divine" :{
  	[h: spellsDb = getLibProperty("divineSpellsDb", "Lib:RQII")]
  };
  case "sorcery" :{
  	[h: spellsDb = getLibProperty("sorcerySpellsDb", "Lib:RQII")]
  };
  case "draconic" :{
  	[h: spellsDb = getLibProperty("draconicSpellsDb", "Lib:RQII")]
  };
  case "draconicPrepared" :{
  	[h: spellsDb = getProperty("draconicSpells", tokenId)]
  };
  default: {
 	<!-- do nothing -->
 	<br>Unknown spell type !<br>
  }
]

[h: link = macroLinkText("openSheetSpellMgt@Lib:RQII", "all")]
<form action="[r:link]" method="json">
  <input type="hidden" name="tokenId" value="[r:tokenId]">
  <table>
    <tr>
       <th>Spell Name</th>
       <th>Spell Type</th>
       <th></th>
    </tr>
    <tr>
      <td><input type="text" name="nameFilter" value="[r: nameFilter]"></td>
      <td>
        <select type="text" name="typeFilter"> 
          <option>common</option>
          <option>divine</option>
          <option>sorcery</option>
          <option>draconic</option>
          <option>spirit</option>
        </select>
      </td>
      <td><input type="submit" name="edit_btn" value="Filter"></td>
    </tr>
  </table>
</form>

[h: link= macroLinkText("addSpell@Lib:RQII", "all")]
<form action="[r:link]" method="json">
<input type="hidden" name="tokenId" value="[r:tokenId]">
<table id="stats" cellpadding="2" cellspacing="0" border="0">
  [h: class = "oddRow"]
  [r, foreach(spell, spellsDb, ""), code:{
	[h: spellValues = json.get(spellsDb,spell)]
	[h: spellType = json.get(spellValues,"type")]
	[h: spellName = json.get(spellValues,"displayname")]
	[r, if(matches(spellName,nameFilter) && matches(spellType,typeFilter)), code:{
	<tr class="[r:class]">
      <td class="leftBorder">
        [r: spellName] (<i>[r: spell]</i>)
      </td>
      <td>
        <input type="checkbox" name="[r:spell]"/>
      </td>
      <td>
        [r: json.get(spellValues,"type")]
      </td>
      <td>
        [r: json.get(spellValues,"grimoire")]
      </td>
      <td>
        [r: json.get(spellValues,"cult")]
      </td>
      <td>
        [r: json.get(spellValues,"magnitude")]
      </td>   
    </tr>
	[h: class = if(class=="oddRow", "evenRow", "oddRow")] 
	};{}]
  }]
</table>
<input type="submit" name="edit_btn" value="Add Spell">
</form>