[h: arguments = macro.args]

[h, if(json.isEmpty(arguments)), code: {
  [tokenId = getSelected()]
  [h: nameFilter = ".*"]
  [h: typeFilter = ".*"]
};{
  [h: tokenId = json.get(arguments,"tokenId")]
  [h: nameFilter = json.get(arguments, "nameFilter")]
  [h: typeFilter = json.get(arguments, "typeFilter")]
}] 

[h, if(listCount(tokenId) != 1): abort(0)]
[h: tokenType = getPropertyType(tokenId)]
[h: assert(((tokenType == "RQ_Human")||(tokenType == "RQ_Monster")),"openAddHeroic - wrong tokenType",0)]

[h: link = macroLinkText("openAddHeroic@Lib:RQII", "")]
[dialog("Add Heroic Ability"): {
  <html>
    <head>
	  <link rel="onChangeSelection" type="macro" href="[r:link]">
      <link rel="stylesheet" type="text/css" href="sheetCss@Lib:RQII">
      <title>Add Heroic Ability - [r: getName(tokenId)]</title>
    </head>
    <body>
    [r, if(isNPC(tokenId) && !isGM()), code: {
      Sorry you cannot edit this token.
    };{
      [h: macroParam = json.set("{}", "tokenId", tokenId,"nameFilter",nameFilter,"typeFilter",typeFilter)]
      [r, macro("editAddHeroic@Lib:RQII"): macroParam]
    }]
    </body>
</html>
}]