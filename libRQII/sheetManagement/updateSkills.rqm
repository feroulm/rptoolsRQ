[r: arguments = macro.args] 
[h: tokenId = json.get(arguments, "tokenId")]
[h: skType = json.get(arguments, "skType")]

[r, switch(skType), code:
  case "common" :{
  	[h: aSkills = getProperty("commonSkills", tokenId)]
  };
  case "advanced" :{
  	[h: aSkills = getProperty("advSkills", tokenId)]
  };
  case "combat" :{
  	[h: aSkills = getProperty("combatSkills", tokenId)]
  };
  case "magic" :{
  	[h: aSkills = getProperty("magicSkills", tokenId)]
  };
  default: {
 	<!-- do nothing -->
 	<br>Unknown skill type !<br>
  }
]

[h: newAdvSkills = "{}"]
<br>
[foreach(skill, aSkills, ""), code: {
	<!-- get existing value -->
	[h: skillValues = json.get(aSkills,skill)]
	[h: skCurrent = json.get(skillValues, "current")]
	[h: skBase = json.get(skillValues, "base")]
	[h: skCulture = json.get(skillValues, "culture")]
	[h: skProfession = json.get(skillValues, "profession")]

	<!-- grab new value from param -->
  	[h: skillName = replace(lower(skill),"[^a-zA-Z0-9_.]","")]
  	[h: newSkSubtype = json.get(arguments, strformat("%{skillName}Subtype"))]
  	[h: newSkCurrent = json.get(arguments, strformat("%{skillName}Current"))]
  	[h: newSkExperience = json.get(arguments, strformat("%{skillName}Experience"))]

  	<!-- validate value, it must be a number -->
    [h, if(json.isEmpty(newSkCurrent)): newSkCurrent = 0]
    [h, if(! isNumber(newSkCurrent)): newSkCurrent = 0]
    [h, if(json.isEmpty(newSkExperience)): newSkExperience = 0]
    [h, if(! isNumber(newSkExperience)): newSkExperience = 0]

    <!-- update subtype -->
    [h: newAdvSkillValues = json.set(skillValues,"subtype",newSkSubtype)]
  	<!-- build the new Json to update advSkills -->
  	[r, if ((newSkExperience == 0)&&(newSkCurrent != 0)), code: {
  		[h: calcNewSkEperience = newSkCurrent - skBase - skCulture - skProfession]
  		[h: newAdvSkillValues = json.set(newAdvSkillValues,"current",newSkCurrent)]
  		[h: newAdvSkillValues = json.set(newAdvSkillValues,"experience",calcNewSkEperience)]
  		[h: newAdvSkills = json.set(newAdvSkills,skill,newAdvSkillValues)]
  		
  		Update [r: skill] : Current to [r: newSkCurrent] and Experience to : [r: calcNewSkEperience]<br>
  		
  	}]
  	[r, if ((newSkExperience != 0)&&(newSkCurrent == 0)), code: {
  		[h: calcNewSkCurrent = skBase + skCulture + skProfession + newSkExperience]

  		[h: newAdvSkillValues = json.set(newAdvSkillValues,"current",calcNewSkCurrent)]
  		[h: newAdvSkillValues = json.set(newAdvSkillValues,"experience",newSkExperience)]
  		[h: newAdvSkills = json.set(newAdvSkills,skill,newAdvSkillValues)]
  		
  		Update [r: skill] : Current to [r: calcNewSkCurrent] and Experience to [r: newSkExperience]<br>
  	}]
  	[r, if ((newSkExperience != 0)&&(newSkCurrent != 0)), code: {

  		[h: newAdvSkills = json.set(newAdvSkills,skill,newAdvSkillValues)]
  		no % update ! use either Current New or Experience New but not both !<br>
  	}]
  	[r, if ((newSkExperience == 0)&&(newSkCurrent == 0)), code: {
  		[h: newAdvSkills = json.set(newAdvSkills,skill,newAdvSkillValues)]
  		no % update ! <br>
  	}]
}]

[r, switch(skType), code:
  case "common" :{
  	[h: setProperty("commonSkills", newAdvSkills, tokenId)]
  };
  case "advanced" :{
  	[h: setProperty("advSkills", newAdvSkills, tokenId)]
  };
  case "combat" :{
  	[h: setProperty("combatSkills", newAdvSkills, tokenId)]
  };
  case "magic" :{
  	[h: setProperty("magicSkills", newAdvSkills, tokenId)]
  };
  default: {
 	<!-- do nothing -->
 	<br>Unknown skill type !<br>
  }
]

<!-- Refresh edit Window for the new token -->
[h: macroParam = json.set("{}", "tokenId", tokenId)]
[r, macro("openSheetMgt@Lib:RQII"): macroParam]