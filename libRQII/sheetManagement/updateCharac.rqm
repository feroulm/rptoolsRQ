[h:"<!-- Update token Charac json properties, it update only the current Value (tmpCharacName) -->"]
[h:"<!-- Called by :  -->"]
[h:"<!-- - editSheet@lib:RQII -->"]
[h:"<!-- - editHumCombatAttr@lib:RQII (sheetManagement Group) -->"]
[h:"<!-- Calling : -->"]
[h:"<!-- - openSheetMgt@Lib:RQII -->"]
[h:"<!-- - openHumCombatAttr@lib:RQII (sheetManagement Group) -->"]

[h: tokenId = json.get(macro.args, "tokenId")]
[h: srcWindow = json.get(macro.args, "srcWindow")]

[h: characNames = "STR, CON, SIZ, INT, POW, DEX, CHA"]
[h: POWded = getProperty("POWded",tokenId)]
[h: propCharac = getProperty("Charac",tokenId)]
[h: propArmorPenalty = getProperty("ArmorPenalty",tokenId)]
[h: propStrikeRank = getProperty("StrikeRank",tokenId)]
[h: propDamageMod = getProperty("DamageMod",tokenId)]
[h: propMagicPoint = getProperty("MagicPoint",tokenId)]
[h: propCombatStatus = getProperty("combatStatus",tokenId)]
[h: damageModTbl = getLibProperty("damageMod","Lib:RQII")]

[h: "<!-- keep previous value to indicate which one was changes -->"]
[h: currSTR = json.get(propCharac,"tmpSTR")]
[h: currCON = json.get(propCharac,"tmpCON")]
[h: currSIZ = json.get(propCharac,"tmpSIZ")]
[h: currINT = json.get(propCharac,"tmpINT")]
[h: currPOW = json.get(propCharac,"tmpPOW")]
[h: currDEX = json.get(propCharac,"tmpDEX")]
[h: currCHA = json.get(propCharac,"tmpCHA")]

[h: newCharac = propCharac]
[h: newSTR = json.get(macro.args,"STR")]
[h: newCON = json.get(macro.args,"CON")]
[h: newSIZ = json.get(macro.args,"SIZ")]
[h: newINT = json.get(macro.args,"INT")]
[h: newPOW = json.get(macro.args,"POW")]
[h: newDEX = json.get(macro.args,"DEX")]
[h: newCHA = json.get(macro.args,"CHA")]

[h: logMsg = "Update "]
[h: logLongMsg = ""]

[h: currActiveCA = json.get(propCombatStatus,"activeCA")]
[h: currCcCA = json.get(propCombatStatus,"ccCA")]

[foreach(characName, characNames, ""), code: {
  [h: "<!-- construct JSON varName based on prop Name -->"]
  [h: tmpCharacName = "tmp"+characName]
  [h: newCharacValue = json.get(macro.args, characName)]
  [h: currCharacValue = json.get(propCharac, tmpCharacName)]
  [h, if (currCharacValue != newCharacValue), code:{
    [h: strformat("%{logLongMsg}<i>%{characName}</i> has changed from %{currCharacValue} to <b>%{newCharacValue}</b>.<br>")]
  	[h: logMsg = logMsg+characName+" "]
  }]
  [h: newCharac = json.set(newCharac,tmpCharacName,newCharacValue)]
  [h: setProperty(characName,newCharacValue,tokenId)]
}]

[h: setProperty("Charac",newCharac,tokenId)]

[h: logCombatAction(tokenId,"change",logMsg)]

[h: "<!-- recompute SR  rounded down (=floor) (INT + DEX) / 2 - Armor Penalty -->"]
[h: currentArmorPenalty = json.get(propArmorPenalty, "current")]
[h: currentSR = json.get(propStrikeRank, "current")]

[h: "<!-- update base value in case DEX or INT changed -->"]
[h: newBaseSR = floor((newINT + newDEX)/2)]
[h: newSR = floor((newINT + newDEX)/2) - currentArmorPenalty]<br>
[h, if(newSR != currentSR), code:{
  [h: strformat("%{logLongMsg} <i>SR</i> has changed from %{currentSR} to <b>%{newSR}</b>, reroll initiative for next mr.<br>")]
};{}]
[h: newStrikeRank = json.set(propStrikeRank,"current",newSR)]
[h: newStrikeRank = json.set(newStrikeRank,"base",newBaseSR)]
[h: setProperty("StrikeRank",newStrikeRank,tokenId)]

[h: "<!-- recompute base CA roundup (=ceil) (DEX+INT)/12 -->"]
[h: currentBaseCA = json.get(propCombatStatus,"baseCA")]
[h: newBaseCA = ceil((newINT + newDEX)/12)]
[h, if(newBaseCA != currentBaseCA), code:{
  [h: strformat("%{logLongMsg} <i>baseCA</i> has changed from %{currentBaseCA} to <b>%{newBaseCA}</b>.<br>")]
  [h: "<!-- recompute ccCA, may impact current CA status -->"]
  [h: newCombatStatus = json.set(propCombatStatus,"baseCA",newBaseCA)]
  [h: newCombatStatus = computeCcCA(tokenId,newCombatStatus)] 
  [h: setProperty("combatStatus",newCombatStatus,tokenId)]
  
  [h: newCcCA = json.get(newCombatStatus,"ccCA")]
  [h: newActiveCA = json.get(newCombatStatus,"activeCA")]
  [h: logLongMsg = strformat("%{logLongMsg}<br><i>ccCA</i> switch from %{currCcCA} to <b>%{newCcCA}</b>.")]
  [h: logLongMsg = strformat("%{logLongMsg} <i>activeCA</i> switch from %{currActiveCA} to <b>%{newActiveCA}</b>.<br>")]
};{}]

[h: "<!-- recompute Damage (FOR+TAI) - table -->"]
[h, if(newSTR+newSIZ>50), code:{
  [h: "<!-- big damage mod progress by range of 10pts -->"]
  [h: newDamageModIndex = ceil((newSTR + newSIZ)/10)+4]
};
{
  [h: "<!-- damage progress mod by range of 5pts -->"]
  [h: newDamageModIndex = ceil((newSTR + newSIZ)/5)-1]
}]

[h, if(newDamageModIndex<0): newDamageModIndex = 0]
[h, if(newDamageModIndex>17), code:{
	[h: newDamageModIndex = 17]
	[h: logLongMsg = strformat("%{logLongMsg}<span style='font-weight: bold;color:red'>Damage Mod when STR + SIZ is above 130 is not implemented...</span>continue the progession<br>")]
}]
[h: newDamageMod = json.get(damageModTbl,newDamageModIndex)]
[h: currentDamageMod = json.get(propDamageMod,"current")]
[h, if(currentDamageMod != newDamageMod), code:{
	[h: logLongMsg = strformat("%{logLongMsg} Damage Mod change from %{currentDamageMod} to <b>%{newDamageMod}</b>.<br>")]
	[h: newPropDamageMod = json.set(propDamageMod,"current",newDamageMod)]
	[h: setProperty("damageMod",newPropDamageMod,tokenId)]
};{}]

[h: "<!-- recompute Magic Point -->"]
[h: "<!-- base MP = newPOW, current MP = currentMP + (newPOW - prevPOW) -->"]
[h, if(newPOW != currPOW), code:{
  [h: newMP = newPOW - json.get(propMagicPoint,"dedPOW")]
  [h: currMP = json.path.read(propMagicPoint,"MP.current")]
  [h: newMagicPoint = json.path.set(propMagicPoint,"MP.base",newMP)]
  [h: newCurrMP = currMP + (newPOW - currPOW)]
  [h: newMagicPoint = json.path.set(newMagicPoint,"MP.current",newCurrMP)]
  [h: setProperty("MagicPoint",newMagicPoint,tokenId)]
  [h: logLongMsg = strformat("%{logLongMsg}Current MP change from %{currMP} to %{newCurrMP}.<br>")]
};{}]

[h: "<!-- recompute HP (CON+SIZ)/2 (rounded up) -->"]
[h: "<!-- - modifiy only hpInit since origHP are computed dynamically base on hpInit. TODO - manage an automatic update #13-->"]
[h: "<!-- - rule : increasing HP during combat could change an existing wound level, it is considered as a magical healing. if it is decreasing it is considered as an injury -->"]
[h: newHpInit = ceil((newCON + newSIZ)/5)]
[h: currHpInit = getProperty("hpInit",tokenId)]
[h, if(newHpInit != currHpInit), code:{
  [h: hpLocChange = newHpInit- currHpInit]
  [h: logLongMsg = strformat("%{logLongMsg} CON or/and SIZ has changed. HP needs to be modified :  add <i>manually</i> %{hpLocChange} in every locations if the token is a human (<i>Check what happens if it is a monster</i>).<br>")]
  [h: setProperty("hpInit",newHpInit,tokenId)]
};{}]

[h: "<!-- recompute Skills -->"]

[h: currSkill = getProperty("commonSkills",tokenId)]
[h: newSkill = computeSkill(currSkill,tokenId)]
[h: setProperty("commonSkills",newSkill,tokenId)]

[h: currSkill = getProperty("combatSkills",tokenId)]
[h: newSkill = computeSkill(currSkill,tokenId)]
[h: setProperty("combatSkills",newSkill,tokenId)]

[h: currSkill = getProperty("magicSkills",tokenId)]
[h: newSkill = computeSkill(currSkill,tokenId)]
[h: setProperty("magicSkills",newSkill,tokenId)]

[h: currSkill = getProperty("advSkills",tokenId)]
[h: newSkill = computeSkill(currSkill,tokenId)]
[h: setProperty("advSkills",newSkill,tokenId)]

[h: macroParam = json.set("{}", "tokenId", tokenId)]

[r: addCombatLogEntry(tokenId,"updateCharac",logLongMsg,"")]

[h, if(srcWindow == "openSheetMgt"), code:{
  [h, macro("openSheetMgt@Lib:RQII"): macroParam]
};{
  [h, macro("openHumCombatAttr@Lib:RQII"): macroParam]
}]
